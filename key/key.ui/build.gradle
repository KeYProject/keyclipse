plugins {
    id 'application'
    
    // Used to create a single executable jar file with all dependencies
    // see task "shadowJar" below
    id 'com.github.johnrengelman.shadow' version "5.0.0"
}

dependencies {
    compile project(":key.core")
    compile project(":key.core:rifl")

    compile project(":key.core.proof_references")
    compile project(":key.core.symbolic_execution")
    compile project(":key.core.testgen")
    compile project(":key.removegenerics")

    compile 'com.miglayout:miglayout-swing:5.2'

    compile files('lib/docking-frames-common.jar',
                'lib/docking-frames-core.jar')

    runtimeOnly project(":keyext.interactionlog")
    //runtimeOnly project(":keyext.script.ui")

    // TODO @AW: Is this not already in key-main?
    compile 'org.jetbrains:annotations:17.0.0'
}

task createExamplesZip(type: Zip) {
    description 'Create "examples.zip" containing all KeY examples'
    destinationDirectory = file("$buildDir/resources/main/")
    archiveFileName = "examples.zip"
    from 'examples'
    include '*'
    include '**/**'
}

processResources.dependsOn << createExamplesZip

shadowJar {
    archiveClassifier = "exe"
    archiveBaseName = "key"
}

distZip {
    archiveBaseName = "key"
}

application {
    // TODO Remove this or enable it?
    // dependsOn(createExamplesZip)
    mainClassName = "de.uka.ilkd.key.core.Main"
}

run {
    systemProperties["key.examples.dir"] = "$projectDir/examples"
    // args "--experimental"
}

