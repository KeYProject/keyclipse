plugins {
    id("ca.coglinc.javacc") version("2.4.0") apply false
}

subprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "java-library"
    apply plugin: "maven-publish"

    group = "org.key_project"
    version = "2.8.0-SNAPSHOT"
    repositories {
        mavenCentral()
        flatDir { dirs "lib", "$rootDir/key.core/lib" }
    }

    dependencies {
        testCompile("junit:junit:4.12")
    }

    sourceSets {
        main {
            java { srcDirs(file("src/")) }
            resources { srcDirs(file("resources/")) }
        }

        test {
            java { srcDirs(file("../${project.name}.test/src/")) }
            resources { srcDirs(file("../${project.name}.test/resources/")) }
        }
    }

    /*val buildInfo by tasks.registering(BuildInfo::class) {
        version = project.version.toString()
        outputFile = file("$buildDir/generated-resources/build-info.properties")
    }*/
}

project(":key.util") {}

project(":key.core.example") {
    dependencies {
        compile project(":key.core")
    }
}
project(":key.core") {
    apply plugin: "ca.coglinc.javacc"
    apply plugin: "antlr"

    dependencies {
        compile project(":key.util")
        compile name: 'recoderKey'
        compile name: 'javacc'
        compile name: 'antlr'

        antlr name: 'antlr'
        javacc name: 'javacc' //'net.java.dev.javacc:javacc:[version]'
        //antlr("org.antlr:antlr:3.5.2")   // use ANTLR version 3
    }

    def javaCCOutputDir = file("${buildDir}/generated-src/javacc")

    sourceSets.main.antlr.srcDirs = [file("src")]
    sourceSets.test.antlr.srcDirs = [file("$rootDir/key.core.test/src/")]
    sourceSets.main.java.srcDirs(javaCCOutputDir)

    generateGrammarSource {
        source = fileTree("src/") {
            include("de/uka/ilkd/key/parser/KeYLexer.g",
                    "de/uka/ilkd/key/speclang/jml/pretranslation/KeYJMLPreParser.g",
                    "de/uka/ilkd/key/speclang/jml/translation/KeYJMLLexer.g",
                    "de/uka/ilkd/key/parser/KeYParser.g",
                    "de/uka/ilkd/key/speclang/jml/translation/KeYJMLParser.g",
                    "de/uka/ilkd/key/speclang/jml/pretranslation/KeYJMLPreLexer.g")
        }

        outputDirectory = file("${projectDir}/build/generated-src/antlr/main")
        arguments.add("-visitor")
    }

    generateTestGrammarSource {
        source = fileTree("$rootDir/key.core.test/src/") {
            include("de/uka/ilkd/key/proof/runallproofs/proofcollection/ProofCollection.g")
        }
        outputDirectory = file("${projectDir}/build/generated-src/antlr/test")
        arguments.add("-visitor")
    }

    compileJavacc {
        outputDirectory = javaCCOutputDir
        inputDirectory = file("src/")
        doLast {
            copy {
                from("src/de/uka/ilkd/key/parser/schemajava/Token.java.source") {
                    rename "Token.java.source", "Token.java"
                }
                into file("${buildDir}/generated-src/javacc/de/uka/ilkd/key/parser/schemajava/")
            }
            copy {
                from("src/de/uka/ilkd/key/parser/proofjava/Token.java.source") {
                    rename "Token.java.source", "Token.java"
                }
                into file("${buildDir}/generated-src/javacc/de/uka/ilkd/key/parser/proofjava/")
            }
        }
        /*
        "/de/uka/ilkd/key/parser/proofjava/ProofJavaParser.jj"
        outputdirectory="${gen.dir}/de/uka/ilkd/key/parser/proofjava"
        javacchome="${ext.dir}"
        -->*/
    }
}


project(":key.core.proof_references") {
    dependencies {
        compile project(":key.core")
    }
}

project(":key.core.symbolic_execution") {
    dependencies {
        compile project(":key.core")
    }
}


project(":key.core.testgen") {
    dependencies {
        compile project(":key.core")
    }
}


project(":key.removegenerics") {
    dependencies {
        compile project(":key.core")
    }
}

project(":key.ui") {
    apply plugin: "application"
    dependencies {
        compile(project(":key.core"))
        compile(project(":key.core.proof_references"))
        compile(project(":key.core.symbolic_execution"))
        compile(project(":key.core.testgen"))
        compile(project(":key.removegenerics"))
    }

    task createExamplesZip(type: Zip) {
        destinationDirectory = file("$projectDir/resources")
        archiveFileName = "examples.zip"
        from 'examples'
        include '*'
        include '*/*' //to include contents of a folder present inside Reports directory
    }

    processResources.dependsOn(createExamplesZip)

    application {
        //dependsOn(createExamplesZip)
        mainClassName = "de.uka.ilkd.key.core.Main"
    }

    run {
        systemProperties["key.examples.dir"] = "$projectDir/examples"
    }
}

