\rules{
  /***********
   *  AXIOMS  *
   ***********/
  /*
   * CharList translateInt (int)
   * - translating an integer into a CharList
   */
  translate0 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(0(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('0',strEmpty)))
    \heuristics (integerToString)
  };

  translate1 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(1(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('1',strEmpty)))
    \heuristics (integerToString)
  };

  translate2 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(2(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('2',strEmpty)))
    \heuristics (integerToString)
  };

  translate3 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(3(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('3',strEmpty)))
    \heuristics (integerToString)
  };

  translate4 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(4(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('4',strEmpty)))
    \heuristics (integerToString)
  };

  translate5 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(5(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('5',strEmpty)))
    \heuristics (integerToString)
  };

  translate6 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(6(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('6',strEmpty)))
    \heuristics (integerToString)
  };

  translate7 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(7(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('7',strEmpty)))
    \heuristics (integerToString)
  };

  translate8 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(8(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('8',strEmpty)))
    \heuristics (integerToString)
  };

  translate9 {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(9(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('9',strEmpty)))
    \heuristics (integerToString)
  };

  translate# {
    \find (translateInt(Z(#)))
    \replacewith (strEmpty)
    \heuristics (integerToString)
  };

  translateNegLit {
    \schemaVar \term numbers iz;
    \find (translateInt(Z(neglit(iz))))
    \replacewith (concat(translateInt(Z(iz)),cons('-',strEmpty)))
    \heuristics (integerToString)
  };

  
  /*
   * CharList removeZeros (CharList) - removing '0' from
   * the beginning of a CharList must not lead to the empty list
   */
  removeZeros {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c1, c2;
    \find (removeZeros(cons(c1,cons(c2,l))))
    \replacewith (\if (c1 = '0') \then (removeZeros(cons(c2,l)))
    		      	   	 \else (cons(c1,cons(c2,l))))
    \heuristics (integerToString)
  };

  removeZerosEmpty {
    \schemaVar \term jchar c;
    \find (removeZeros(cons(c,strEmpty)))
    \replacewith (cons(c,strEmpty))
    \heuristics (integerToString)
  };

 /*
   * int charAt (int,CharList)
   * an index < 0 and >= strLength(l) in unspecified
   */
  charAtBase {
    \schemaVar \term CharList str;
    \schemaVar \term jchar fstChar;
    
    \find ( charAt(0, cons(fstChar, str) ) )
    \replacewith  ( fstChar )
 
    \heuristics ( simplify_literals )
  };
   
  charAtObserverDef {
     \schemaVar \term CharList str;
     \schemaVar \term jchar fstChar;
     \schemaVar \term int pos;
     
     \schemaVar \skolemTerm jchar newSym;
    
     \find ( charAt(pos, str) )
     \sameUpdateLevel         
     \varcond ( \new(newSym, \dependingOn(pos)),
     	        \new(newSym, \dependingOn(str)) )
     \add (
     	(pos >= 0 & pos < strLength(str) & str != strEmpty) ->  
     	   ( pos > 0 -> charAt(pos, str) = newSym & charAt(0, substring(pos, pos + 1, str)) = newSym )     	 
     	     ==>
     )
     \heuristics ( defOpsCharAt )
  };
  
  /*
   * = (CharList,CharList)nonNe
   * the equality predicate
   */

  /*
   * Equality is also axiomatized using charAt...
   */
  stringEqualityDefinitionSucc {
    // strategy: not both leftStr and rightStr are literals 

    \schemaVar \term CharList leftStr, rightStr;
    \schemaVar \variables int idx;
    
    \find ( ==> leftStr = rightStr )
    \varcond ( \notFreeIn(idx, leftStr), \notFreeIn(idx, rightStr) )
    \add ( ==>
          strLength(leftStr) = strLength(rightStr) &         
          \forall idx; ((idx >= 0 & idx < strLength(leftStr))
                         -> charAt(idx, leftStr) = charAt(idx, rightStr)) )
    
    \heuristics (defOpsStringEquality)
  };

  stringEqualityRecursiveDef {
    \schemaVar \term CharList leftStr, rightStr;
    \schemaVar \term jchar fstCharLeft, fstCharRight;
    
    \find (cons(fstCharLeft, leftStr) = cons(fstCharRight, rightStr))
    \replacewith (\if (fstCharLeft = fstCharRight) 
    		  \then (leftStr = rightStr)
    		  \else (false))
    \heuristics ( defOpsStringEqualityInline ) 
  };
  
  stringEqualityFalse0 {
    \schemaVar \term CharList str;
    \schemaVar \term jchar fstChar;
    
    \find ( cons(fstChar, str) = strEmpty )
    \replacewith ( false )
    \heuristics ( concrete )
  };

  stringEqualityFalse1 {
    \schemaVar \term CharList str;
    \schemaVar \term jchar fstChar;
    
    \find ( strEmpty = cons(fstChar, str) )
    \replacewith ( false )
    \heuristics ( concrete )
  };

  /*
   * CharList substring(int,int,CharList)
   * from start index to end index
   * The axiom substring0 uses the indirection through concat,
   * so it does not create a cons term that is not in value form.
   */
  substringBase { 
    //strategy: apply if idx is nonNegLiteral
    // in case idx is not a non neg literal, but a term 
    // the this case will be taken care of by substringDef followed by that strEmpty is the only 
    // string with zero length 
    
    \schemaVar \term CharList str;
    \schemaVar \term int idx;
    
    \find ( substring(idx, idx, str) )
    \replacewith ( 
         \if ( idx >= 0 & idx <= strLength(str) ) 
         \then ( strEmpty )
         \else ( substring(idx, idx, str) ) )
    
    \heuristics ( defOpsSubstringInlineBase )
  };
  
  substringStepCons { //strategy: apply if endIdx is posliteral
    \schemaVar \term CharList str;
    \schemaVar \term jchar fstChar;
    \schemaVar \term int endIdx;
    
    \find ( substring (0, endIdx, cons(fstChar, str)) )
    \replacewith ( \if (endIdx > 0)
                   \then (concat(cons(fstChar, strEmpty), substring(0, endIdx - 1, str)))
                   \else (substring(0, endIdx, cons (fstChar, str))) )
    \heuristics ( defOpsSubstringInlineStepCons )
  };

  substringInline { //strategy: apply if startIdx is literal and endIdx
    \schemaVar \term CharList str;
    \schemaVar \term jchar fstChar;
    \schemaVar \term int startIdx, endIdx;
    
    \find ( substring (startIdx, endIdx, cons(fstChar, str)) )
    \replacewith ( 
    	\if (startIdx > 0 & startIdx <= endIdx) 
    	\then (substring(startIdx - 1, endIdx - 1, str))
    	\else (substring(startIdx, endIdx, cons(fstChar, str))) 
    )
    \heuristics ( defOpsSubstringInline )
  };

  substringDef { //strategy: apply if startIdx is literal
    \schemaVar \term CharList str;
    \schemaVar \skolemTerm CharList newSym;
    \schemaVar \term jchar fstChar;
    \schemaVar \term int startIdx, endIdx;
    \schemaVar \variables int pos;
    
    
    \find ( substring (startIdx, endIdx, str) )
    \sameUpdateLevel
    \varcond ( \new(newSym, \dependingOn(startIdx)), 
               \new(newSym, \dependingOn(endIdx)),
               \new(newSym, \dependingOn(str)),
               \notFreeIn(pos, startIdx, endIdx, str) )
    \add ( 
        ( startIdx >=0       & 
          startIdx <= endIdx & 
          endIdx <= strLength (str) ) 
      ->
        ( substring (startIdx, endIdx, str) = newSym & 
          strLength(newSym) = endIdx - startIdx         &
          \forall pos; ( 
                ( pos >= startIdx & pos < endIdx )
                     -> charAt(pos, str) = charAt(pos, newSym)
          )
        ) ==> )
    \heuristics ( defOpsSubstring, stringsIntroduceNewSym )
  };
  
  
  /*
   * CharList concat (CharList,CharList)
   * concatenate the two lists
   */
  concatEmpty1 {
    \schemaVar \term CharList str;
    \find ( concat(strEmpty, str) )
    \replacewith ( str )
    \heuristics ( simplify_literals )
  };

  concatEmpty2 {
    \schemaVar \term CharList str;
    \find ( concat(str, strEmpty) )
    \replacewith ( str )
    \heuristics ( simplify_literals )
  };
  
  concatCons {
    \schemaVar \term CharList leftStr, rightStr;
    \schemaVar \term jchar fstCharLeft, fstCharRight;

    \find ( concat(cons(fstCharLeft, leftStr), cons(fstCharRight, rightStr)) )
    
    \replacewith ( cat(cons(fstCharLeft, strEmpty), 
                       concat(leftStr, cons(fstCharRight, rightStr))) )

    \heuristics ( simplify_literals )
  };
  

  concatConsDef {
    // strategy, apply only once and only if leftStr or rightStr is not a literal  
    \schemaVar \term CharList leftStr, rightStr;
    \schemaVar \skolemTerm CharList newSym;
    
    \find ( concat(leftStr, rightStr) )
    \sameUpdateLevel    
    \varcond ( \new(newSym, \dependingOn(leftStr)) ,
      	       \new(newSym, \dependingOn(rightStr)) )
    \add (
	concat(leftStr, rightStr) = newSym &
        strLength(newSym) = strLength (leftStr) + strLength (rightStr) &
        substring(0, strLength(leftStr), newSym) = leftStr &
        substring(strLength(leftStr), strLength(newSym), newSym) = rightStr 
    	==> 
    )

    \heuristics ( defOpsConcat, stringsIntroduceNewSym )
  };


/** concat should not built terms, where a cons subterm is
     not in value form so this axiom should be used with care **/
  concatConsDep {
    \schemaVar \term CharList l1, l2;
    \schemaVar \term jchar c;

    \find (concat(cons(c,l1),l2))
    \replacewith (cons(c,concat(l1,l2)))
  
  };

  cat {
    \schemaVar \term CharList str;
    \schemaVar \term jchar fstCharLeft, fstCharRight;
    
    \find ( cat(cons(fstCharLeft, strEmpty), cons(fstCharRight, str)) )
    \replacewith ( cons(fstCharLeft, cons(fstCharRight, str)) )
    
    \heuristics ( simplify_literals )
  };

  /*
   * int strLength(CharList)
   */
  lengthEmpty {
    \find ( strLength(strEmpty) )
    \replacewith (0)
    
    \heuristics ( simplify_literals )
  };

  lengthCons {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c;
    
    \find ( strLength(cons(c, l)) )
    \replacewith ( strLength(l) + 1 )
    
    \heuristics ( simplify_literals )
  };
   
  /*
   * contains (CharList,CharList)
   * predicate indicating whether the first list
   * is contained in the second
   */
  containsAxiomAntec {
    \schemaVar \term CharList searchString, textString;
    \schemaVar \variables int iv;

    \find ( contains(searchString, textString) ==> )
    \varcond ( \notFreeIn (iv, searchString), \notFreeIn (iv, textString) )
    \replacewith ( \exists iv; (  iv >= 0
                               & iv + strLength(searchString) <= strLength(textString)
                               & substring(iv, iv + strLength(searchString), textString) = searchString) ==> )
    
    \heuristics ( stringsExpandDefNormalOp )
  };

  containsAxiomSucc {
    \schemaVar \term CharList searchString, textString;
    \schemaVar \variables int iv;

    \find (==> contains(searchString, textString) )
    \varcond ( \notFreeIn (iv, searchString), \notFreeIn (iv, textString) )
    \replacewith ( ==> \exists iv; (  iv >= 0
                       	            & iv + strLength(searchString) <= strLength(textString)
                                    & substring(iv, iv + strLength(searchString), textString) = searchString) )
    
    \heuristics ( stringsExpandDefNormalOp )
  };
  
  
   contains {
    \schemaVar \term CharList searchString, textStringTail;
    \schemaVar \term jchar fstTextCharacter;
    \schemaVar \skolemTerm int newSym;    
    
    \find (contains(searchString, cons(fstTextCharacter, textStringTail) ) )
    \sameUpdateLevel
    \varcond ( \new(newSym, \dependingOn(searchString) ) )    
    \replacewith ( newSym < strLength(textStringTail) & 
                     ( substring(0, newSym, cons(fstTextCharacter, textStringTail)) = searchString | 
                       contains(searchString, textStringTail) ) )
    \add ( strLength(searchString) = newSym ==>)
                               
    \heuristics ( stringsContainsDefInline, stringsIntroduceNewSym )
    
  };

 
  /*
   * structural induction over all CharLists
   */
  charlist_induction {
    \schemaVar \variables CharList lv;
    \schemaVar \variables jchar cv;
    \schemaVar \formula phi;
    \varcond ( \notFreeIn(cv,phi)  )
    "Base Case": \add ( ==> {\subst lv; strEmpty}(phi) );
    "Step Case": \add ( ==> \forall lv; \forall cv;
                              ((inChar(cv) & phi)
                               -> {\subst lv; cons(cv,lv)}phi) );
    "Use Case":  \add ( \forall lv; (phi) ==> )
  };

  /*
   * jchar indexOf (jchar,int,CharList)
   */
  indexOf {
    \schemaVar \term CharList l;
    \schemaVar \term jchar c;
    \schemaVar \term int i;
    \schemaVar \variables int iv;
    \find (indexOf (c,i,l))
    \varcond ( \notFreeIn(iv,l),
               \notFreeIn(iv,c),
               \notFreeIn(iv,i) )
    \replacewith ( \ifEx iv; (  iv >= 0
                              & iv >= i
                              & iv < strLength(l)
                              & charAt(iv,l) = c )
                     \then (iv)
                     \else (-1) )
    \heuristics ( stringsExpandDefNormalOp )
  };


  
  /*
   * CharList replace (jchar,jchar,CharList)
   * replace all occurences of the first character
   * with the second character
   */
  replaceEmpty {
    \schemaVar \term jchar searchChar, replChar;
    
    \find ( replace(searchChar, replChar, strEmpty))
    \replacewith ( strEmpty )
    \heuristics ( stringsSimplify )
  };

  replaceCons {
    \schemaVar \term CharList str;
    \schemaVar \term jchar searchChar, replChar, fstChar;
    
    \find ( replace(searchChar, replChar, cons(fstChar, str)) )
    \replacewith ( \if (searchChar = fstChar)
                   \then (concat(cons(replChar, strEmpty),
                                 replace(searchChar, replChar, str)))
                   \else (concat(cons(fstChar, strEmpty),
                                 replace(searchChar, replChar, str))) )
    \heuristics ( defOpsReplaceInline )
  };
  
  replaceDef {
    \schemaVar \term CharList str;
    \schemaVar \skolemTerm CharList newSym;
    \schemaVar \term jchar searchChar, replChar;
    \schemaVar \variables int pos;
    
    \find ( replace(searchChar, replChar, str) )
    \sameUpdateLevel
    \varcond ( \notFreeIn(pos, str, searchChar, replChar),
               \new(newSym, \dependingOn(searchChar)),
               \new(newSym, \dependingOn(replChar)),
               \new(newSym, \dependingOn(str)) )  
    \add ( replace(searchChar, replChar, str) = newSym &
       strLength (newSym) = strLength(str) & 
       \forall pos; ( ( pos >= 0 & pos < strLength(str) ) ->  
               \if ( charAt (pos, str) = searchChar)
               \then ( charAt (pos, newSym) = replChar )
               \else ( charAt (pos, str) = charAt (pos, newSym) ) 
       ) ==> )
    
    \heuristics ( defOpsReplace, stringsIntroduceNewSym )
  };
  
  
  /*
   * endsWith(CharList,CharList)
   * predicate indicating if the first list
   * is a suffix of the second one
   */
  endsWith {
    \schemaVar \term CharList l1, l2;
    
    \find (endsWith(l1,l2))
    \replacewith ( \if (strLength(l1) > strLength(l2))
    		   \then (false)
		   \else (substring(strLength(l2)-strLength(l1),
                                    strLength(l2),l2) = l1) )
    
    \heuristics ( defOpsStartsEndsWith )
  };

  /*
   * startsWith(CharList,CharList)
   * predicate indicating if the first list
   * is a prefix of the second one
   */
  startsWith {
    \schemaVar \term CharList l1,l2;
    \find(startsWith(l1,l2))
    \replacewith ( \if (strLength(l1)>strLength(l2))
    		   \then (false)
		   \else (substring(0,strLength(l1),l2) = l1) )
    
    \heuristics ( defOpsStartsEndsWith )
  };

  /*
   * int indexOfStr (CharList,int,CharList)
   */
  indexOfStr {
    \schemaVar \term CharList l1, l2;
    \schemaVar \term int i;
    \schemaVar \variables int iv;
    \find (indexOfStr(l1,i,l2))
    \varcond ( \notFreeIn (iv,l1), \notFreeIn (iv,l2), \notFreeIn (iv,i) )
    \replacewith ( \ifEx iv; (iv >= i
    		   	      & iv >= 0
    		   	      & iv+strLength(l1) <= strLength(l2)
			      & (substring(iv,iv+strLength(l1),l2) = l1))
                   \then (iv)
                   \else (-1) )
    \heuristics ( stringsExpandDefNormalOp )    
  };

  /*
   * int lastIndexOf (jchar,int,CharList)
   */
  lastIndexOf {
      \schemaVar \term CharList l;
      \schemaVar \term jchar c;
      \schemaVar \term int i;
      \schemaVar \variables int iv;
      \find (lastIndexOf (c,i,l))
      \varcond ( \notFreeIn (iv,c), \notFreeIn (iv,i), \notFreeIn (iv,l) )
      \replacewith (\ifEx iv; (  iv > 0
                                & i-iv >= 0
                                & i-iv < strLength(l)
                                & charAt(i-iv,l) = c)
                    \then (i - iv)
                    \else (-1))
      \heuristics ( stringsExpandDefNormalOp )  
  };

  /*
   * int lastIndexOfStr (CharList,int,CharList)
   */
  lastIndexOfStr {
      \schemaVar \term CharList l1,l2;
      \schemaVar \term int i;
      \schemaVar \variables int iv;
      \find (lastIndexOfStr (l1,i,l2))
      \varcond ( \notFreeIn (iv,l1), \notFreeIn (iv,i), \notFreeIn (iv,l2) )
      \replacewith (\ifEx iv; (  iv > 0
                                & i-iv >= 0
                                & strLength(l1)+i-iv <= strLength(l2)
                                & (substring(i-iv,strLength(l1)+i-iv,l2) = l1))
                    \then (i - iv)
                    \else (-1))
      \heuristics ( stringsExpandDefNormalOp )
  };

 

  /**
   * we define only the base case
   */
  hashCodeBase {
    \find ( hashCode(strEmpty) )
    \replacewith ( 0 )
    \heuristics ( simplify_literals )
  };

  /************
   *  LEMMAS  *
   ************/
   
  substringIdentity0 {
    \schemaVar \term CharList l;
    \schemaVar \term int len;    

    \assumes (strLength(l) = len ==>)
    \find ( substring(0, len, l) )
    \sameUpdateLevel
    \replacewith ( l )
    \heuristics ( stringsSimplify )

    \displayname "substringIdentity"
  };


  substringIdentity1 {
    \schemaVar \term CharList str;

    \find ( substring(0, strLength(str), str) )
    \replacewith ( str )
    \heuristics ( stringsSimplify )
    
    \displayname "substringIdentity"
  };

   /*
    * strEmpty is the only list having length 0
    */
   zeroLengthIsEmptyString { // strategy: apply only once
    \schemaVar \term CharList str;
    
    \find ( strLength(str) = 0 ==> )
    \add ( str = strEmpty ==> )
    \heuristics ( stringsLengthInvariant )
  };

  /*
   * The length of every CharList is at least 0
   */
  lengthNonNegative {
      \schemaVar \term CharList str;
  
      \find (strLength(str)) 
      \sameUpdateLevel
      \add (strLength(str) >= 0 ==> )
      \heuristics ( stringsLengthInvariant )
  };  
  
  /*
   * The length of a concatenation
   */
  lengthConcat {
      \schemaVar \term CharList leftStr, rightStr, concatStr;
      
      \assumes ( concat(leftStr, rightStr) = concatStr ==>)
      \find ( strLength(concatStr) )
      \sameUpdateLevel      
      \replacewith ( strLength(leftStr) + strLength(rightStr) )
      \heuristics ( stringsExpandLengthConcat )
      
      \displayname "lengthConcat"
  };

  /*
   * A replace application will not change
   * the length of the string
   */
  lengthReplace {
      \schemaVar \term CharList str, newStr;
      \schemaVar \term jchar searchChar, replaceChar;
      
      \assumes ( replace(searchChar, replaceChar, str) = newStr ==>)      
      \find ( strLength(newStr) )
      \sameUpdateLevel
      \replacewith( strLength(str) )
      \heuristics ( stringsSimplify )
  };

  /*
   * charAt applied on a concat will either refer
   * to the first or the second subterm
   */
  charAtConcat {
      \schemaVar \term CharList leftStr,rightStr;
      \schemaVar \term int charPos;
      
      \find ( charAt(charPos, concat(leftStr, rightStr)))
      \replacewith ( \if (charPos >= 0 & charPos < strLength(leftStr) + strLength(rightStr))
                     \then (\if (charPos < strLength(leftStr))
                             \then (charAt(charPos, leftStr))
                             \else (charAt(charPos - strLength(leftStr), rightStr)))
                     \else (charAt(charPos, concat(leftStr, rightStr))) )
      \heuristics ( stringsConcatNotBothLiterals, stringsReduceConcat )
  };

  /*
   * Two succedent substring calls can be combined to one
   */
  substringSubstring {
      \schemaVar \term CharList str;
      \schemaVar \skolemTerm CharList newSym;
      \schemaVar \term int outerStartIdx, outerEndIdx, innerStartIdx, innerEndIdx;
      
      \find ( substring(outerStartIdx, outerEndIdx, substring(innerStartIdx, innerEndIdx, str)) )
      \sameUpdateLevel
      
      \varcond ( \new(newSym, \dependingOn(str)),
                 \new(newSym, \dependingOn(outerStartIdx)), 
                 \new(newSym, \dependingOn(outerEndIdx)), 
                 \new(newSym, \dependingOn(innerStartIdx)) ) 
      \add ( (innerStartIdx >= 0 
      	      & innerEndIdx >= innerStartIdx
              & innerEndIdx <= strLength(str)
              & outerStartIdx >= 0
              & outerEndIdx >= outerStartIdx 
              & outerEndIdx <= innerEndIdx - innerStartIdx) 
            -> 
             (
             substring(outerStartIdx, outerEndIdx, substring(innerStartIdx, innerEndIdx, str)) = newSym & 
             substring(outerStartIdx + innerStartIdx, innerStartIdx + outerEndIdx, str) = newSym) ==> )      
      \heuristics ( stringsReduceSubstring, stringsIntroduceNewSym )      
  };

  substringSubstring2 {
      \schemaVar \term CharList str, innerSub;
      \schemaVar \skolemTerm CharList newSym;
      \schemaVar \term int outerStartIdx, outerEndIdx, innerStartIdx, innerEndIdx;
      
      \assumes ( substring(innerStartIdx, innerEndIdx, str) = innerSub ==> )
      \find ( substring(outerStartIdx, outerEndIdx, innerSub) )
      \sameUpdateLevel      
      \varcond ( \new(newSym, \dependingOn(str)),
                 \new(newSym, \dependingOn(outerStartIdx)), 
                 \new(newSym, \dependingOn(outerEndIdx)), 
                 \new(newSym, \dependingOn(innerStartIdx)) ) 
      \add ( (innerStartIdx >= 0 
      	      & innerEndIdx >= innerStartIdx
              & innerEndIdx <= strLength(str)
              & outerStartIdx >= 0
              & outerEndIdx >= outerStartIdx 
              & outerEndIdx <= innerEndIdx - innerStartIdx) 
            -> 
             ( substring(outerStartIdx, outerEndIdx, innerSub) = newSym & 
               substring(outerStartIdx + innerStartIdx, innerStartIdx + outerEndIdx, str) = newSym ) ==> )      
      \heuristics ( stringsReduceSubstring, stringsIntroduceNewSym )      
  };


  /*
   * l is not equal to cons(c,l)
   */
  consEq {
      \schemaVar \term CharList str;
      \schemaVar \term jchar fstChar;
      \find ( str = cons(fstChar, str) )
      \replacewith ( false )
      \heuristics ( stringsSimplify )
  };

  /*
   * concatenation of a non empty string
   * changes the original string
   */
  concatEqLeft {
      \schemaVar \term CharList leftStr, rightStr;
      \assumes ( ==> leftStr = strEmpty)
      \find ( concat(leftStr, rightStr) = rightStr )
      \sameUpdateLevel
      \replacewith ( false )
      \heuristics ( stringsSimplify )
  };
  
  concatEqRight {
      \schemaVar \term CharList leftStr, rightStr;
      \assumes ( ==> rightStr = strEmpty)
      \find ( concat(leftStr, rightStr) = leftStr )
      \sameUpdateLevel
      \replacewith ( false )
      \heuristics ( stringsSimplify )
  };
  
  equalReduce {
    \schemaVar \term CharList l1, l2;
    \schemaVar \term jchar c;
    \find (cons(c,l1) = cons(c,l2))
    \replacewith (l1 = l2)
    \heuristics ( stringsSimplify ) 
  };
  
  equalCharacters {
    \schemaVar \term numbers iz1, iz2;
    \find ( C(iz1) = C(iz2) )
    \replacewith ( Z(iz1) = Z(iz2) )
    \heuristics ( stringsSimplify ) 
  };
  

// CONTINUE (rules below are sound, but optimal?)

  substringConcat {
      \schemaVar \term CharList leftStr, rightStr;
      \schemaVar \term int startIdx, endIdx;
      
      \find ( substring(startIdx, endIdx, concat(leftStr, rightStr)) )
      \replacewith ( \if (  startIdx >= 0 & endIdx >= startIdx
                          & endIdx <= strLength(leftStr) + strLength(rightStr))
                     \then (\if (endIdx <= strLength(leftStr))
                            \then (substring(startIdx, endIdx, leftStr))
                            \else (\if (startIdx >= strLength(leftStr))
                                   \then (substring(
                                           startIdx - strLength(leftStr),
                                           endIdx - strLength(leftStr), rightStr))
                                   \else (concat(
                                           substring(startIdx,
                                                     strLength(leftStr),
                                                     leftStr),
                                           substring(0,
                                                     endIdx - strLength(leftStr),
                                                     rightStr)))))
                     \else (substring(startIdx, endIdx, concat(leftStr, rightStr))) )
      \heuristics ( stringsReduceOrMoveOutsideConcat, stringsConcatNotBothLiterals )      
  };

  substringConcat2 {
      \schemaVar \term CharList leftStr, rightStr, concatStr;
      \schemaVar \term int startIdx, endIdx;

      \assumes ( concat(leftStr, rightStr) = concatStr ==> )      
      \find ( substring(startIdx, endIdx, concatStr) )
      \replacewith ( \if (  startIdx >= 0 & endIdx >= startIdx
                          & endIdx <= strLength(concatStr) )
                     \then (\if (endIdx <= strLength(leftStr))
                            \then (substring(startIdx, endIdx, leftStr))
                            \else (\if (startIdx >= strLength(leftStr))
                                   \then (substring(
                                           startIdx - strLength(leftStr),
                                           endIdx - strLength(leftStr),rightStr))
                                   \else (concat(
                                           substring(startIdx,
                                                     strLength(leftStr),
                                                     leftStr),
                                           substring(0,
                                                     endIdx - strLength(leftStr),
                                                     rightStr)))))
                     \else (substring(startIdx, endIdx, concatStr)) )
      \heuristics ( stringsReduceOrMoveOutsideConcat, stringsConcatNotBothLiterals )      
  
      \displayname "substringConcat"
  };

  concatConcat {
      \schemaVar \term CharList outerLeft, innerLeft, innerRight;
      
      \find ( concat(outerLeft, concat(innerLeft, innerRight)) )
      \replacewith ( concat(concat(outerLeft, innerLeft), innerRight) )
      
      \heuristics ( stringsSimplify )      
  };

  replaceSubstring {
      \schemaVar \term CharList str, subStr;
      \schemaVar \term jchar searchChar, replaceChar;
      \schemaVar \term int startIdx, endIdx;

      \assumes (substring(startIdx, endIdx, str) = subStr ==>)

      \find (replace(searchChar, replaceChar, subStr))
      \replacewith (\if (startIdx >= 0 & endIdx >= startIdx & endIdx <= strLength(str))
                    \then (substring(startIdx, endIdx, replace(searchChar, replaceChar, str)))
                    \else (replace(searchChar, replaceChar, subStr)) )
      \heuristics ( stringsMoveReplaceInside )      

      \displayname "replaceSubstring"      
  };

  /*
   * A replace inside a concat application
   * does not affect the concat...
   */
  replaceConcat {
      \schemaVar \term CharList leftStr, rightStr;
      \schemaVar \term jchar searchChar, replaceChar;
      \find (replace(searchChar, replaceChar, concat(leftStr, rightStr)))
      \replacewith (concat(replace(searchChar, replaceChar, leftStr),
                           replace(searchChar, replaceChar, rightStr)))
      \heuristics ( stringsMoveReplaceInside )      
   };
}

