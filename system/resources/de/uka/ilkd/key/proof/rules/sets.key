\sorts {
    Set;
}

\functions {
    Set empty;
    Set singleton(any);
    Set union(Set, Set);
    Set intersect(Set, Set);
    Set setMinus(Set, Set);
    Set setComprehension{true, false}(any, Set);    
}

\predicates {
    elementOf(any, Set);
    subset(Set, Set);
    disjoint(Set, Set);
}


\rules {
 
    //--------------------------------------------------------------------------
    //axioms for elementOf
    //--------------------------------------------------------------------------
   
    elementOfEmpty {
        \schemaVar \term any e;
        
        \find(elementOf(e, empty))
        
        \replacewith(false)
        
        \heuristics(concrete)
    };
    

    elementOfSingleton {
        \schemaVar \term any e, e2;
        
        \find(elementOf(e, singleton(e2)))
        
        \replacewith(e = e2)
        
        \heuristics(simplify)
    };
    
    
    elementOfUnion {
        \schemaVar \term any e;
        \schemaVar \term Set s, s2;
        
        \find(elementOf(e, union(s, s2)))
        
        \replacewith(elementOf(e, s) | elementOf(e, s2))
        
        \heuristics(simplify)
    };
    
    
    elementOfIntersect {
        \schemaVar \term any e;
        \schemaVar \term Set s, s2;
        
        \find(elementOf(e, intersect(s, s2)))
        
        \replacewith(elementOf(e, s) & elementOf(e, s2))
        
        \heuristics(simplify)
    };
    
        
    elementOfSetMinus {
        \schemaVar \term any e;
        \schemaVar \term Set s, s2;
        
        \find(elementOf(e, setMinus(s, s2)))
        
        \replacewith(elementOf(e, s) & !elementOf(e, s2))
        
        \heuristics(simplify)
    };
    
    
    elementOfSetComprehension {
        \schemaVar \term any e, t;
	\schemaVar \variables alpha av;
	\schemaVar \term Set s;
        
        \find(elementOf(e, setComprehension{av;}(t, s)))
        \varcond(\notFreeIn(av, s), \notFreeIn(av, e))
        
        \replacewith(\exists av; (elementOf(t, s) & t = e))
        
        \heuristics(simplify)
    };
    
    
    elementOfSetComprehension2Vars {
        \schemaVar \term any e, t;
	\schemaVar \variables alpha av;
	\schemaVar \variables beta bv;	
	\schemaVar \term Set s;
        
        \find(elementOf(e, setComprehension{av, bv;}(t, s)))
        \varcond(\notFreeIn(av, s), 
                 \notFreeIn(av, e), 
                 \notFreeIn(bv, s), 
                 \notFreeIn(bv, e))
        
        \replacewith(\exists av; \exists bv; (elementOf(t, s) & t = e))
        
        \heuristics(simplify)
    };    
    
    

    //--------------------------------------------------------------------------
    //EQ versions of axioms (these are lemmata)
    //--------------------------------------------------------------------------
       
    elementOfEmptyEQ {
        \schemaVar \term any e;
        \schemaVar \term Set EQ;
        
        \assumes(empty = EQ ==>)        
        \find(elementOf(e, EQ))
        \sameUpdateLevel
        
        \replacewith(false)
        
        \heuristics(concrete)
    };
    

    elementOfSingletonEQ {
        \schemaVar \term any e, e2;
        \schemaVar \term Set EQ;
        
        \assumes(singleton(e2) = EQ ==>)
        \find(elementOf(e, EQ))        
        \sameUpdateLevel
        
        \replacewith(e = e2)
        
        \heuristics(simplify)
    };
    
    
    elementOfUnionEQ {
        \schemaVar \term any e;
        \schemaVar \term Set s, s2;
        \schemaVar \term Set EQ;
        
        \assumes(union(s, s2) = EQ ==>)
        \find(elementOf(e, EQ))               
        \sameUpdateLevel
        
        \replacewith(elementOf(e, s) | elementOf(e, s2))
        
        \heuristics(simplify)
    };
    
    
    elementOfIntersectEQ {
        \schemaVar \term any e;
        \schemaVar \term Set s, s2;
        \schemaVar \term Set EQ;
        
        \assumes(intersect(s, s2) = EQ ==>)
        \find(elementOf(e, EQ))
        \sameUpdateLevel
        
        \replacewith(elementOf(e, s) & elementOf(e, s2))
        
        \heuristics(simplify)
    };
    
        
    elementOfSetMinusEQ {
        \schemaVar \term any e;
        \schemaVar \term Set s, s2;
        \schemaVar \term Set EQ;
        
        \assumes(setMinus(s, s2) = EQ ==>)
        \find(elementOf(e, EQ))        
        \sameUpdateLevel
        
        \replacewith(elementOf(e, s) & !elementOf(e, s2))
        
        \heuristics(simplify)
    };
    
    
    elementOfSetComprehensionEQ {
        \schemaVar \term any e, t;
	\schemaVar \variables alpha av;
	\schemaVar \term Set s;
        \schemaVar \term Set EQ;
        
        \assumes(setComprehension{av;}(t, s) = EQ ==>)        
        \find(elementOf(e, EQ))
	\sameUpdateLevel
        \varcond(\notFreeIn(av, s), \notFreeIn(av, e))
        
        \replacewith(\exists av; (elementOf(t, s) & t = e))
        
        \heuristics(simplify)
    };
    
    
    elementOfSetComprehension2VarsEQ {
        \schemaVar \term any e, t;
	\schemaVar \variables alpha av;
    	\schemaVar \variables beta bv;		
	\schemaVar \term Set s;
        \schemaVar \term Set EQ;
        
        \assumes(setComprehension{av, bv;}(t, s) = EQ ==>)        
        \find(elementOf(e, EQ))
	\sameUpdateLevel
        \varcond(\notFreeIn(av, s), 
                 \notFreeIn(av, e), 
                 \notFreeIn(bv, s), 
                 \notFreeIn(bv, e))
        
        \replacewith(\exists av; \exists bv; (elementOf(t, s) & t = e))
        
        \heuristics(simplify)
    };    
        
    
    //--------------------------------------------------------------------------
    //axioms for equality, subset, disjoint (reduce to elementOf)
    //--------------------------------------------------------------------------
    
    equalityToElementOf {
    	\schemaVar \term Set s, s2;
	\schemaVar \variables any av;    	
    	
    	\find(s = s2)
	\varcond(\notFreeIn(av, s, s2))
	
    	\replacewith(\forall av; (elementOf(av, s) <-> elementOf(av, s2)))
    };


    subsetToElementOf {
        \schemaVar \term Set s, s2;
	\schemaVar \variables any av;    	
        
        \find(subset(s, s2))
	\varcond(\notFreeIn(av, s, s2))
	
	\replacewith(\forall av; (elementOf(av, s) -> elementOf(av, s2)))
	
        \heuristics(simplify)
    };

    
    disjointToElementOf {
        \schemaVar \term Set s, s2;
	\schemaVar \variables any av;    	
        
        \find(disjoint(s, s2))
	\varcond(\notFreeIn(av, s, s2))
	
        \replacewith(\forall av; (!elementOf(av, s) | !elementOf(av, s2)))
        
        \heuristics(simplify)
    };


    
    //--------------------------------------------------------------------------
    //lemmata for empty
    //--------------------------------------------------------------------------
    
    unionWithEmpty1 {
        \schemaVar \term Set s;
        
        \find(union(s, empty))
        
        \replacewith(s)
        
        \heuristics(concrete)
    };
    
    
    unionWithEmpty2 {
        \schemaVar \term Set s;
        
        \find(union(empty, s))
        
        \replacewith(s)
        
        \heuristics(concrete)
    };
    
    
    intersectWithEmpty1 {
        \schemaVar \term Set s;
        
        \find(intersect(s, empty))
        
        \replacewith(empty)
        
        \heuristics(concrete)
    };


    intersectWithEmpty2 {
        \schemaVar \term Set s;
        
        \find(intersect(empty, s))
        
        \replacewith(empty)
        
        \heuristics(concrete)
    };


    setMinusWithEmpty1 {
        \schemaVar \term Set s;
        
        \find(setMinus(s, empty))
        
        \replacewith(s)
        
        \heuristics(concrete)
    };


    setMinusWithEmpty2 {
        \schemaVar \term Set s;
        
        \find(setMinus(empty, s))
        
        \replacewith(empty)
        
        \heuristics(concrete)
    };
        
    
    subsetWithEmpty {
        \schemaVar \term Set s;
        
        \find(subset(empty, s))
        
        \replacewith(true)
        
        \heuristics(concrete)
    };
    
    
    disjointWithEmpty1 {
        \schemaVar \term Set s;
        
        \find(disjoint(empty, s))
        
        \replacewith(true)
        
        \heuristics(concrete)
    };
    
    
    disjointWithEmpty2 {
        \schemaVar \term Set s;
        
        \find(disjoint(s, empty))
        
        \replacewith(true)
        
        \heuristics(concrete)
    };
    
    
        
    //--------------------------------------------------------------------------
    //some other lemmata
    //--------------------------------------------------------------------------
    
    unionWithItself {
        \schemaVar \term Set s;
        
        \find(union(s, s))
        
        \replacewith(s)
        
        \heuristics(concrete)
    };
    
    
    intersectWithItself {
        \schemaVar \term Set s;
        
        \find(intersect(s, s))
        
        \replacewith(s)
        
        \heuristics(concrete)
    };    
    
    
    setMinusItself {
        \schemaVar \term Set s;
        
        \find(setMinus(s, s))
        
        \replacewith(empty)
        
        \heuristics(concrete)
    };
    
    
    setComprehensionUnused {
	\schemaVar \variables alpha av;
        \schemaVar \term any t;	
	\schemaVar \term Set s;
        
        \find(setComprehension{av;}(t, s))
        \varcond(\notFreeIn(av, t), \notFreeIn(av, s))
        
        \replacewith(\if(elementOf(t,s))\then(singleton(t))\else(empty))
        
        \heuristics(simplify)
    };
    
    
    subsetOfItself {
        \schemaVar \term Set s;
        
        \find(subset(s, s))
        
        \replacewith(true)
        
        \heuristics(concrete)    
    };
}
