\rules (Strings:on) {
  /***********
   *  AXIOMS *
   ***********/
  /*
   * CharList clTranslateInt (int)
   * - translating an integer into a CharList
   */
  translate0 {
    \schemaVar \term numbers iz;
    \find (clTranslateInt(Z(0(iz))))
    \replacewith (clConcat(clTranslateInt(Z(iz)),clCons('0',clEmpty)))
    \heuristics (integerToString)
  };

  translate1 {
    \schemaVar \term numbers iz;
    \find (clTranslateInt(Z(1(iz))))
    \replacewith (clConcat(clTranslateInt(Z(iz)),clCons('1',clEmpty)))
    \heuristics (integerToString)
  };

  translate2 {
    \schemaVar \term numbers iz;
    \find (clTranslateInt(Z(2(iz))))
    \replacewith (clConcat(clTranslateInt(Z(iz)),clCons('2',clEmpty)))
    \heuristics (integerToString)
  };

  translate3 {
    \schemaVar \term numbers iz;
    \find (clTranslateInt(Z(3(iz))))
    \replacewith (clConcat(clTranslateInt(Z(iz)),clCons('3',clEmpty)))
    \heuristics (integerToString)
  };

  translate4 {
    \schemaVar \term numbers iz;
    \find (clTranslateInt(Z(4(iz))))
    \replacewith (clConcat(clTranslateInt(Z(iz)),clCons('4',clEmpty)))
    \heuristics (integerToString)
  };

  translate5 {
    \schemaVar \term numbers iz;
    \find (clTranslateInt(Z(5(iz))))
    \replacewith (clConcat(clTranslateInt(Z(iz)),clCons('5',clEmpty)))
    \heuristics (integerToString)
  };

  translate6 {
    \schemaVar \term numbers iz;
    \find (clTranslateInt(Z(6(iz))))
    \replacewith (clConcat(clTranslateInt(Z(iz)),clCons('6',clEmpty)))
    \heuristics (integerToString)
  };

  translate7 {
    \schemaVar \term numbers iz;
    \find (clTranslateInt(Z(7(iz))))
    \replacewith (clConcat(clTranslateInt(Z(iz)),clCons('7',clEmpty)))
    \heuristics (integerToString)
  };

  translate8 {
    \schemaVar \term numbers iz;
    \find (clTranslateInt(Z(8(iz))))
    \replacewith (clConcat(clTranslateInt(Z(iz)),clCons('8',clEmpty)))
    \heuristics (integerToString)
  };

  translate9 {
    \schemaVar \term numbers iz;
    \find (clTranslateInt(Z(9(iz))))
    \replacewith (clConcat(clTranslateInt(Z(iz)),clCons('9',clEmpty)))
    \heuristics (integerToString)
  };

  translate# {
    \find (clTranslateInt(Z(#)))
    \replacewith (clEmpty)
    \heuristics (integerToString)
  };

  translateNegLit {
    \schemaVar \term numbers iz;
    \find (clTranslateInt(Z(neglit(iz))))
    \replacewith (clCons('-', clTranslateInt(Z(iz))))
    \heuristics (integerToString)
  };

  
  /*
   * CharList clRemoveZeros (CharList) - removing '0' from
   * the beginning of a CharList must not lead to the empty list
   */
  removeZeros {
    \schemaVar \term CharList l;
    \schemaVar \term int c1, c2;
    \find (clRemoveZeros(clCons(c1,clCons(c2,l))))
    \replacewith (\if (c1 = '0') \then (clRemoveZeros(clCons(c2,l)))
    		      	   	 \else (clCons(c1,clCons(c2,l))))
    \heuristics (integerToString)
  };
  
  removeZerosEmpty {
    \schemaVar \term int c;
    \find (clRemoveZeros(clCons(c,clEmpty)))
    \replacewith (clCons(c,clEmpty))
    \heuristics (integerToString)
  };

 /*
   * int clCharAt (int,CharList)
   * an index < 0 and >= clLength(l) in unspecified
   */
  charAtBase {
    \schemaVar \term CharList str;
    \schemaVar \term int fstChar;
    
    \find ( clCharAt(0, clCons(fstChar, str) ) )
    \replacewith  ( fstChar )
 
    \heuristics ( simplify_literals )
  };
   
  charAtObserverDef {
     \schemaVar \term CharList str;
     \schemaVar \term int fstChar;
     \schemaVar \term int pos;
     
     \schemaVar \skolemTerm int newSym;
    
     \find ( clCharAt(pos, str) )
     \sameUpdateLevel         
     \varcond ( \new(newSym, \dependingOn(pos)),
     	        \new(newSym, \dependingOn(str)) )
     \add (
     	(pos >= 0 & pos < clLength(str) & str != clEmpty) ->  
     	   ( pos > 0 -> clCharAt(pos, str) = newSym & clCharAt(0, clSub(pos, pos + 1, str)) = newSym )     	 
     	     ==>
     )
     \heuristics ( defOpsCharAt )
  };
  
  /*
   * = (CharList,CharList)nonNe
   * the equality predicate
   */

  /*
   * Equality is also axiomatized using clCharAt...
   */
  stringEqualityDefinitionSucc {
    // strategy: not both leftStr and rightStr are literals 

    \schemaVar \term CharList leftStr, rightStr;
    \schemaVar \variables int idx;
    
    \find ( ==> leftStr = rightStr )
    \varcond ( \notFreeIn(idx, leftStr), \notFreeIn(idx, rightStr) )
    \add ( ==>
          clLength(leftStr) = clLength(rightStr) &         
          \forall idx; ((idx >= 0 & idx < clLength(leftStr))
                         -> clCharAt(idx, leftStr) = clCharAt(idx, rightStr)) )
    
    \heuristics (defOpsStringEquality)
  };

  stringEqualityRecursiveDef {
    \schemaVar \term CharList leftStr, rightStr;
    \schemaVar \term int fstCharLeft, fstCharRight;
    
    \find (clCons(fstCharLeft, leftStr) = clCons(fstCharRight, rightStr))
    \replacewith (\if (fstCharLeft = fstCharRight) 
    		  \then (leftStr = rightStr)
    		  \else (false))
    \heuristics ( defOpsStringEqualityInline ) 
  };
  
  stringEqualityFalse0 {
    \schemaVar \term CharList str;
    \schemaVar \term int fstChar;
    
    \find ( clCons(fstChar, str) = clEmpty )
    \replacewith ( false )
    \heuristics ( concrete )
  };

  stringEqualityFalse1 {
    \schemaVar \term CharList str;
    \schemaVar \term int fstChar;
    
    \find ( clEmpty = clCons(fstChar, str) )
    \replacewith ( false )
    \heuristics ( concrete )
  };

  /*
   * CharList clSub(int,int,CharList)
   * from start index to end index
   * The axiom substring0 uses the indirection through clConcat,
   * so it does not create a clCons term that is not in value form.
   */
  substringBase { 
    //strategy: apply if idx is nonNegLiteral
    // in case idx is not a non neg literal, but a term 
    // the this case will be taken care of by substringDef followed by that clEmpty is the only 
    // string with zero length 
    
    \schemaVar \term CharList str;
    \schemaVar \term int idx;
    
    \find ( clSub(idx, idx, str) )
    \replacewith ( 
         \if ( idx >= 0 & idx <= clLength(str) ) 
         \then ( clEmpty )
         \else ( clSub(idx, idx, str) ) )
    
    \heuristics ( defOpsSubstringInlineBase )
  };
  
  substringStepCons { //strategy: apply if endIdx is posliteral
    \schemaVar \term CharList str;
    \schemaVar \term int fstChar;
    \schemaVar \term int endIdx;
    
    \find ( clSub (0, endIdx, clCons(fstChar, str)) )
    \replacewith ( \if (endIdx > 0)
                   \then (clConcat(clCons(fstChar, clEmpty), clSub(0, endIdx - 1, str)))
                   \else (clSub(0, endIdx, clCons (fstChar, str))) )
    \heuristics ( defOpsSubstringInlineStepCons )
  };

  substringInline { //strategy: apply if startIdx is literal and endIdx
    \schemaVar \term CharList str;
    \schemaVar \term int fstChar;
    \schemaVar \term int startIdx, endIdx;
    
    \find ( clSub (startIdx, endIdx, clCons(fstChar, str)) )
    \replacewith ( 
    	\if (startIdx > 0 & startIdx <= endIdx) 
    	\then (clSub(startIdx - 1, endIdx - 1, str))
    	\else (clSub(startIdx, endIdx, clCons(fstChar, str))) 
    )
    \heuristics ( defOpsSubstringInline )
  };

  substringDef { //strategy: apply if startIdx is literal
    \schemaVar \term CharList str;
    \schemaVar \skolemTerm CharList newSym;
    \schemaVar \term int fstChar;
    \schemaVar \term int startIdx, endIdx;
    \schemaVar \variables int pos;
    
    
    \find ( clSub (startIdx, endIdx, str) )
    \sameUpdateLevel
    \varcond ( \new(newSym, \dependingOn(startIdx)), 
               \new(newSym, \dependingOn(endIdx)),
               \new(newSym, \dependingOn(str)),
               \notFreeIn(pos, startIdx, endIdx, str) )
    \add ( 
        ( startIdx >=0       & 
          startIdx <= endIdx & 
          endIdx <= clLength (str) ) 
      ->
        ( clSub (startIdx, endIdx, str) = newSym & 
          clLength(newSym) = endIdx - startIdx         &
          \forall pos; ( 
                ( pos >= startIdx & pos < endIdx )
                     -> clCharAt(pos, str) = clCharAt(pos, newSym)
          )
        ) ==> )
    \heuristics ( defOpsSubstring, stringsIntroduceNewSym )
  };
  
  
  /*
   * CharList clConcat (CharList,CharList)
   * concatenate the two lists
   */
  concatEmpty1 {
    \schemaVar \term CharList str;
    \find ( clConcat(clEmpty, str) )
    \replacewith ( str )
    \heuristics ( simplify_literals )
  };

  concatEmpty2 {
    \schemaVar \term CharList str;
    \find ( clConcat(str, clEmpty) )
    \replacewith ( str )
    \heuristics ( simplify_literals )
  };
  
  concatCons {
    \schemaVar \term CharList leftStr, rightStr;
    \schemaVar \term int fstCharLeft, fstCharRight;

    \find ( clConcat(clCons(fstCharLeft, leftStr), clCons(fstCharRight, rightStr)) )
    
    \replacewith ( clCat(clCons(fstCharLeft, clEmpty), 
                       clConcat(leftStr, clCons(fstCharRight, rightStr))) )

    \heuristics ( simplify_literals )
  };
  

  concatConsDef {
    // strategy, apply only once and only if leftStr or rightStr is not a literal  
    \schemaVar \term CharList leftStr, rightStr;
    \schemaVar \skolemTerm CharList newSym;
    
    \find ( clConcat(leftStr, rightStr) )
    \sameUpdateLevel    
    \varcond ( \new(newSym, \dependingOn(leftStr)) ,
      	       \new(newSym, \dependingOn(rightStr)) )
    \add (
	clConcat(leftStr, rightStr) = newSym &
        clLength(newSym) = clLength (leftStr) + clLength (rightStr) &
        clSub(0, clLength(leftStr), newSym) = leftStr &
        clSub(clLength(leftStr), clLength(newSym), newSym) = rightStr 
    	==> 
    )

    \heuristics ( defOpsConcat, stringsIntroduceNewSym )
  };


/** concat should not built terms, where a clCons subterm is
     not in value form so this axiom should be used with care **/
  concatConsDep {
    \schemaVar \term CharList l1, l2;
    \schemaVar \term int c;

    \find (clConcat(clCons(c,l1),l2))
    \replacewith (clCons(c,clConcat(l1,l2)))
  
  };

  cat {
    \schemaVar \term CharList str;
    \schemaVar \term int fstCharLeft, fstCharRight;
    
    \find ( clCat(clCons(fstCharLeft, clEmpty), clCons(fstCharRight, str)) )
    \replacewith ( clCons(fstCharLeft, clCons(fstCharRight, str)) )
    
    \heuristics ( simplify_literals )
  };

  /*
   * int clLength(CharList)
   */
  lengthEmpty {
    \find ( clLength(clEmpty) )
    \replacewith (0)
    
    \heuristics ( simplify_literals )
  };

  lengthCons {
    \schemaVar \term CharList l;
    \schemaVar \term int c;
    
    \find ( clLength(clCons(c, l)) )
    \replacewith ( clLength(l) + 1 )
    
    \heuristics ( simplify_literals )
  };
   
  /*
   * clContains (CharList,CharList)
   * predicate indicating whether the first list
   * is contained in the second
   */
  containsAxiomAntec {
    \schemaVar \term CharList searchString, textString;
    \schemaVar \variables int iv;

    \find ( clContains(searchString, textString) ==> )
    \varcond ( \notFreeIn (iv, searchString), \notFreeIn (iv, textString) )
    \replacewith ( \exists iv; (  iv >= 0
                               & iv + clLength(searchString) <= clLength(textString)
                               & clSub(iv, iv + clLength(searchString), textString) = searchString) ==> )
    
    \heuristics ( stringsExpandDefNormalOp )
  };

  containsAxiomSucc {
    \schemaVar \term CharList searchString, textString;
    \schemaVar \variables int iv;

    \find (==> clContains(searchString, textString) )
    \varcond ( \notFreeIn (iv, searchString), \notFreeIn (iv, textString) )
    \replacewith ( ==> \exists iv; (  iv >= 0
                       	            & iv + clLength(searchString) <= clLength(textString)
                                    & clSub(iv, iv + clLength(searchString), textString) = searchString) )
    
    \heuristics ( stringsExpandDefNormalOp )
  };
  
  
   contains {
    \schemaVar \term CharList searchString, textStringTail;
    \schemaVar \term int fstTextCharacter;
    \schemaVar \skolemTerm int newSym;    
    
    \find (clContains(searchString, clCons(fstTextCharacter, textStringTail) ) )
    \sameUpdateLevel
    \varcond ( \new(newSym, \dependingOn(searchString) ) )    
    \replacewith ( newSym < clLength(textStringTail) & 
                     ( clSub(0, newSym, clCons(fstTextCharacter, textStringTail)) = searchString | 
                       clContains(searchString, textStringTail) ) )
    \add ( clLength(searchString) = newSym ==>)
                               
    \heuristics ( stringsContainsDefInline, stringsIntroduceNewSym )
    
  };

 
  /*
   * structural induction over all CharLists
   */
  charlist_induction {
    \schemaVar \variables CharList lv;
    \schemaVar \variables int cv;
    \schemaVar \formula phi;
    \varcond ( \notFreeIn(cv,phi)  )
    "Base Case": \add ( ==> {\subst lv; clEmpty}(phi) );
    "Step Case": \add ( ==> \forall lv; \forall cv;
                              ((inChar(cv) & phi)
                               -> {\subst lv; clCons(cv,lv)}phi) );
    "Use Case":  \add ( \forall lv; (phi) ==> )
  };

  /*
   * int clIndexOfChar (int,int,CharList)
   */
  indexOf {
    \schemaVar \term CharList l;
    \schemaVar \term int c;
    \schemaVar \term int i;
    \schemaVar \variables int iv;
    \find (clIndexOfChar (c,i,l))
    \varcond ( \notFreeIn(iv,l),
               \notFreeIn(iv,c),
               \notFreeIn(iv,i) )
    \replacewith ( \ifEx iv; (  iv >= 0
                              & iv >= i
                              & iv < clLength(l)
                              & clCharAt(iv,l) = c )
                     \then (iv)
                     \else (-1) )
    \heuristics ( stringsExpandDefNormalOp )
  };


  
  /*
   * CharList clReplace (int,int,CharList)
   * replace all occurrences of the first character
   * with the second character
   */
  replaceEmpty {
    \schemaVar \term int searchChar, replChar;
    
    \find ( clReplace(searchChar, replChar, clEmpty))
    \replacewith ( clEmpty )
    \heuristics ( stringsSimplify )
  };

  replaceCons {
    \schemaVar \term CharList str;
    \schemaVar \term int searchChar, replChar, fstChar;
    
    \find ( clReplace(searchChar, replChar, clCons(fstChar, str)) )
    \replacewith ( \if (searchChar = fstChar)
                   \then (clConcat(clCons(replChar, clEmpty),
                                 clReplace(searchChar, replChar, str)))
                   \else (clConcat(clCons(fstChar, clEmpty),
                                 clReplace(searchChar, replChar, str))) )
    \heuristics ( defOpsReplaceInline )
  };
  
  replaceDef {
    \schemaVar \term CharList str;
    \schemaVar \skolemTerm CharList newSym;
    \schemaVar \term int searchChar, replChar;
    \schemaVar \variables int pos;
    
    \find ( clReplace(searchChar, replChar, str) )
    \sameUpdateLevel
    \varcond ( \notFreeIn(pos, str, searchChar, replChar),
               \new(newSym, \dependingOn(searchChar)),
               \new(newSym, \dependingOn(replChar)),
               \new(newSym, \dependingOn(str)) )  
    \add ( clReplace(searchChar, replChar, str) = newSym &
       clLength (newSym) = clLength(str) & 
       \forall pos; ( ( pos >= 0 & pos < clLength(str) ) ->  
               \if ( clCharAt (pos, str) = searchChar)
               \then ( clCharAt (pos, newSym) = replChar )
               \else ( clCharAt (pos, str) = clCharAt (pos, newSym) ) 
       ) ==> )
    
    \heuristics ( defOpsReplace, stringsIntroduceNewSym )
  };
  
  
  /*
   * clEndsWith(CharList,CharList)
   * predicate indicating if the first list
   * is a suffix of the second one
   */
  endsWith {
    \schemaVar \term CharList l1, l2;
    
    \find (clEndsWith(l1,l2))
    \replacewith ( \if (clLength(l1) > clLength(l2))
    		   \then (false)
		   \else (clSub(clLength(l2)-clLength(l1),
                                    clLength(l2),l2) = l1) )
    
    \heuristics ( defOpsStartsEndsWith )
  };

  /*
   * clStartsWith(CharList,CharList)
   * predicate indicating if the first list
   * is a prefix of the second one
   */
  startsWith {
    \schemaVar \term CharList l1,l2;
    \find(clStartsWith(l1,l2))
    \replacewith ( \if (clLength(l1)>clLength(l2))
    		   \then (false)
		   \else (clSub(0,clLength(l1),l2) = l1) )
    
    \heuristics ( defOpsStartsEndsWith )
  };

  /*
   * int clIndexOfCl (CharList,int,CharList)
   */
  indexOfStr {
    \schemaVar \term CharList l1, l2;
    \schemaVar \term int i;
    \schemaVar \variables int iv;
    \find (clIndexOfCl(l1,i,l2))
    \varcond ( \notFreeIn (iv,l1), \notFreeIn (iv,l2), \notFreeIn (iv,i) )
    \replacewith ( \ifEx iv; (iv >= i
    		   	      & iv >= 0
    		   	      & iv+clLength(l1) <= clLength(l2)
			      & (clSub(iv,iv+clLength(l1),l2) = l1))
                   \then (iv)
                   \else (-1) )
    \heuristics ( stringsExpandDefNormalOp )    
  };

  /*
   * int clLastIndexOfChar (int,int,CharList)
   */
  lastIndexOf {
      \schemaVar \term CharList l;
      \schemaVar \term int c;
      \schemaVar \term int i;
      \schemaVar \variables int iv;
      \find (clLastIndexOfChar (c,i,l))
      \varcond ( \notFreeIn (iv,c), \notFreeIn (iv,i), \notFreeIn (iv,l) )
      \replacewith (\ifEx iv; (  iv > 0
                                & i-iv >= 0
                                & i-iv < clLength(l)
                                & clCharAt(i-iv,l) = c)
                    \then (i - iv)
                    \else (-1))
      \heuristics ( stringsExpandDefNormalOp )  
  };

  /*
   * int clLastIndexOfCl (CharList,int,CharList)
   */
  lastIndexOfStr {
      \schemaVar \term CharList l1,l2;
      \schemaVar \term int i;
      \schemaVar \variables int iv;
      \find (clLastIndexOfCl (l1,i,l2))
      \varcond ( \notFreeIn (iv,l1), \notFreeIn (iv,i), \notFreeIn (iv,l2) )
      \replacewith (\ifEx iv; (  iv > 0
                                & i-iv >= 0
                                & clLength(l1)+i-iv <= clLength(l2)
                                & (clSub(i-iv,clLength(l1)+i-iv,l2) = l1))
                    \then (i - iv)
                    \else (-1))
      \heuristics ( stringsExpandDefNormalOp )
  };

 

  /**
   * we define only the base case
   */
  hashCodeBase {
    \find ( clHashCode(clEmpty) )
    \replacewith ( 0 )
    \heuristics ( simplify_literals )
  };

  /************
   *  LEMMAS  *
   ************/
   
  substringIdentity0 {
    \schemaVar \term CharList l;
    \schemaVar \term int len;    

    \assumes (clLength(l) = len ==>)
    \find ( clSub(0, len, l) )
    \sameUpdateLevel
    \replacewith ( l )
    \heuristics ( stringsSimplify )

    \displayname "substringIdentity"
  };


  substringIdentity1 {
    \schemaVar \term CharList str;

    \find ( clSub(0, clLength(str), str) )
    \replacewith ( str )
    \heuristics ( stringsSimplify )
    
    \displayname "substringIdentity"
  };

   /*
    * clEmpty is the only list having length 0
    */
   zeroLengthIsEmptyString { // strategy: apply only once
    \schemaVar \term CharList str;
    
    \find ( clLength(str) = 0 ==> )
    \add ( str = clEmpty ==> )
    \heuristics ( stringsLengthInvariant )
  };

  /*
   * The length of every CharList is at least 0
   */
  lengthNonNegative {
      \schemaVar \term CharList str;
  
      \find (clLength(str)) 
      \sameUpdateLevel
      \add (clLength(str) >= 0 ==> )
      \heuristics ( stringsLengthInvariant )
  };  
  
  /*
   * The length of a concatenation
   */
  lengthConcat {
      \schemaVar \term CharList leftStr, rightStr, concatStr;
      
      \assumes ( clConcat(leftStr, rightStr) = concatStr ==>)
      \find ( clLength(concatStr) )
      \sameUpdateLevel      
      \replacewith ( clLength(leftStr) + clLength(rightStr) )
      \heuristics ( stringsExpandLengthConcat )
      
      \displayname "lengthConcat"
  };

  /*
   * A clReplace application will not change
   * the length of the string
   */
  lengthReplace {
      \schemaVar \term CharList str, newStr;
      \schemaVar \term int searchChar, replaceChar;
      
      \assumes ( clReplace(searchChar, replaceChar, str) = newStr ==>)      
      \find ( clLength(newStr) )
      \sameUpdateLevel
      \replacewith( clLength(str) )
      \heuristics ( stringsSimplify )
  };

  /*
   * clCharAt applied on a concat will either refer
   * to the first or the second subterm
   */
  charAtConcat {
      \schemaVar \term CharList leftStr,rightStr;
      \schemaVar \term int charPos;
      
      \find ( clCharAt(charPos, clConcat(leftStr, rightStr)))
      \replacewith ( \if (charPos >= 0 & charPos < clLength(leftStr) + clLength(rightStr))
                     \then (\if (charPos < clLength(leftStr))
                             \then (clCharAt(charPos, leftStr))
                             \else (clCharAt(charPos - clLength(leftStr), rightStr)))
                     \else (clCharAt(charPos, clConcat(leftStr, rightStr))) )
      \heuristics ( stringsConcatNotBothLiterals, stringsReduceConcat )
  };

  /*
   * Two succedent substring calls can be combined to one
   */
  substringSubstring {
      \schemaVar \term CharList str;
      \schemaVar \skolemTerm CharList newSym;
      \schemaVar \term int outerStartIdx, outerEndIdx, innerStartIdx, innerEndIdx;
      
      \find ( clSub(outerStartIdx, outerEndIdx, clSub(innerStartIdx, innerEndIdx, str)) )
      \sameUpdateLevel
      
      \varcond ( \new(newSym, \dependingOn(str)),
                 \new(newSym, \dependingOn(outerStartIdx)), 
                 \new(newSym, \dependingOn(outerEndIdx)), 
                 \new(newSym, \dependingOn(innerStartIdx)) ) 
      \add ( (innerStartIdx >= 0 
      	      & innerEndIdx >= innerStartIdx
              & innerEndIdx <= clLength(str)
              & outerStartIdx >= 0
              & outerEndIdx >= outerStartIdx 
              & outerEndIdx <= innerEndIdx - innerStartIdx) 
            -> 
             (
             clSub(outerStartIdx, outerEndIdx, clSub(innerStartIdx, innerEndIdx, str)) = newSym & 
             clSub(outerStartIdx + innerStartIdx, innerStartIdx + outerEndIdx, str) = newSym) ==> )      
      \heuristics ( stringsReduceSubstring, stringsIntroduceNewSym )      
  };

  substringSubstring2 {
      \schemaVar \term CharList str, innerSub;
      \schemaVar \skolemTerm CharList newSym;
      \schemaVar \term int outerStartIdx, outerEndIdx, innerStartIdx, innerEndIdx;
      
      \assumes ( clSub(innerStartIdx, innerEndIdx, str) = innerSub ==> )
      \find ( clSub(outerStartIdx, outerEndIdx, innerSub) )
      \sameUpdateLevel      
      \varcond ( \new(newSym, \dependingOn(str)),
                 \new(newSym, \dependingOn(outerStartIdx)), 
                 \new(newSym, \dependingOn(outerEndIdx)), 
                 \new(newSym, \dependingOn(innerStartIdx)) ) 
      \add ( (innerStartIdx >= 0 
      	      & innerEndIdx >= innerStartIdx
              & innerEndIdx <= clLength(str)
              & outerStartIdx >= 0
              & outerEndIdx >= outerStartIdx 
              & outerEndIdx <= innerEndIdx - innerStartIdx) 
            -> 
             ( clSub(outerStartIdx, outerEndIdx, innerSub) = newSym & 
               clSub(outerStartIdx + innerStartIdx, innerStartIdx + outerEndIdx, str) = newSym ) ==> )      
      \heuristics ( stringsReduceSubstring, stringsIntroduceNewSym )      
  };


  /*
   * l is not equal to clCons(c,l)
   */
  consEq {
      \schemaVar \term CharList str;
      \schemaVar \term int fstChar;
      \find ( str = clCons(fstChar, str) )
      \replacewith ( false )
      \heuristics ( stringsSimplify )
  };

  /*
   * concatenation of a non empty string
   * changes the original string
   */
  concatEqLeft {
      \schemaVar \term CharList leftStr, rightStr;
      \assumes ( ==> leftStr = clEmpty)
      \find ( clConcat(leftStr, rightStr) = rightStr )
      \sameUpdateLevel
      \replacewith ( false )
      \heuristics ( stringsSimplify )
  };
  
  concatEqRight {
      \schemaVar \term CharList leftStr, rightStr;
      \assumes ( ==> rightStr = clEmpty)
      \find ( clConcat(leftStr, rightStr) = leftStr )
      \sameUpdateLevel
      \replacewith ( false )
      \heuristics ( stringsSimplify )
  };
  
  equalReduce {
    \schemaVar \term CharList l1, l2;
    \schemaVar \term int c;
    \find (clCons(c,l1) = clCons(c,l2))
    \replacewith (l1 = l2)
    \heuristics ( stringsSimplify ) 
  };
  
  equalCharacters {
    \schemaVar \term numbers iz1, iz2;
    \find ( C(iz1) = C(iz2) )
    \replacewith ( Z(iz1) = Z(iz2) )
    \heuristics ( stringsSimplify ) 
  };
  

// CONTINUE (rules below are sound, but optimal?)

  substringConcat {
      \schemaVar \term CharList leftStr, rightStr;
      \schemaVar \term int startIdx, endIdx;
      
      \find ( clSub(startIdx, endIdx, clConcat(leftStr, rightStr)) )
      \replacewith ( \if (  startIdx >= 0 & endIdx >= startIdx
                          & endIdx <= clLength(leftStr) + clLength(rightStr))
                     \then (\if (endIdx <= clLength(leftStr))
                            \then (clSub(startIdx, endIdx, leftStr))
                            \else (\if (startIdx >= clLength(leftStr))
                                   \then (clSub(
                                           startIdx - clLength(leftStr),
                                           endIdx - clLength(leftStr), rightStr))
                                   \else (clConcat(
                                           clSub(startIdx,
                                                     clLength(leftStr),
                                                     leftStr),
                                           clSub(0,
                                                     endIdx - clLength(leftStr),
                                                     rightStr)))))
                     \else (clSub(startIdx, endIdx, clConcat(leftStr, rightStr))) )
      \heuristics ( stringsReduceOrMoveOutsideConcat, stringsConcatNotBothLiterals )      
  };

  substringConcat2 {
      \schemaVar \term CharList leftStr, rightStr, concatStr;
      \schemaVar \term int startIdx, endIdx;

      \assumes ( clConcat(leftStr, rightStr) = concatStr ==> )      
      \find ( clSub(startIdx, endIdx, concatStr) ) \sameUpdateLevel
      \replacewith ( \if (  startIdx >= 0 & endIdx >= startIdx
                          & endIdx <= clLength(concatStr) )
                     \then (\if (endIdx <= clLength(leftStr))
                            \then (clSub(startIdx, endIdx, leftStr))
                            \else (\if (startIdx >= clLength(leftStr))
                                   \then (clSub(
                                           startIdx - clLength(leftStr),
                                           endIdx - clLength(leftStr),rightStr))
                                   \else (clConcat(
                                           clSub(startIdx,
                                                     clLength(leftStr),
                                                     leftStr),
                                           clSub(0,
                                                     endIdx - clLength(leftStr),
                                                     rightStr)))))
                     \else (clSub(startIdx, endIdx, concatStr)) )
      \heuristics ( stringsReduceOrMoveOutsideConcat, stringsConcatNotBothLiterals )      
  
      \displayname "substringConcat"
  };

  concatConcat {
      \schemaVar \term CharList outerLeft, innerLeft, innerRight;
      
      \find ( clConcat(outerLeft, clConcat(innerLeft, innerRight)) )
      \replacewith ( clConcat(clConcat(outerLeft, innerLeft), innerRight) )
      
      \heuristics ( stringsSimplify )      
  };

  replaceSubstring {
      \schemaVar \term CharList str, subStr;
      \schemaVar \term int searchChar, replaceChar;
      \schemaVar \term int startIdx, endIdx;

      \assumes (clSub(startIdx, endIdx, str) = subStr ==>)

      \find (clReplace(searchChar, replaceChar, subStr)) \sameUpdateLevel
      \replacewith (\if (startIdx >= 0 & endIdx >= startIdx & endIdx <= clLength(str))
                    \then (clSub(startIdx, endIdx, clReplace(searchChar, replaceChar, str)))
                    \else (clReplace(searchChar, replaceChar, subStr)) )
      \heuristics ( stringsMoveReplaceInside )      

      \displayname "replaceSubstring"      
  };

  /*
   * A clReplace inside a clConcat application
   * does not affect the clConcat...
   */
  replaceConcat {
      \schemaVar \term CharList leftStr, rightStr;
      \schemaVar \term int searchChar, replaceChar;
      \find (clReplace(searchChar, replaceChar, clConcat(leftStr, rightStr)))
      \replacewith (clConcat(clReplace(searchChar, replaceChar, leftStr),
                           clReplace(searchChar, replaceChar, rightStr)))
      \heuristics ( stringsMoveReplaceInside )      
   };
}
